def currentComponent = rootProject.ext.components.main
if (currentComponent.isStandalone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    def versions = rootProject.ext.versions
    compileSdkVersion versions.compileSdkVersion


    defaultConfig {
        if (currentComponent.isStandalone) {
            // 当前组件独立运行，需要设置applicationId
            applicationId currentComponent.applicationId
        }

        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def moduleName = project.getName()
        resourcePrefix "${moduleName}_"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        // ARouter处理器所需参数
                        moduleName   : moduleName,
                        // Component处理器所需参数
                        componentName: moduleName
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (currentComponent.isStandalone) {
                manifest.srcFile 'src/standalone/AndroidManifest.xml'
                java.srcDirs = ['src/maincomponent_main/java/', 'src/standalone/java/']
                res.srcDirs = ['src/maincomponent_main/res', 'src/standalone/res']
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    def dependencies = rootProject.ext.dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation dependencies.support.appcompat
    implementation dependencies.support.design

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    annotationProcessor dependencies.arouter.annotationProcessor
    annotationProcessor project(':component-compiler')
    api project(':componentservice')
}
